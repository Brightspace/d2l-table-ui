@groups
	imageCompare small.head,small.body,small.foot,simple.head,simple.body,simple.foot,no-column-border.head,no-column-border.body,notbody
	all          &imageCompare,wide,sort,rows,rows-styled,no-column-border-styled.head,no-column-border-styled.body

== D2L Table ==

	@forEach [&all] as part
		${part}.tr-1:
			aligned horizontally top ${part}

		@forEach [${part}.tr-*] as tr, prev as prevTr
			${tr}:
				below ${prevTr} 0px

	@forEach [&imageCompare] as part
		${part}:
			image file dumps/${path}/objects/${part}.png, error 12px

		@forEach [${part}.tr-*] as tr
			${tr}:
				image file dumps/${path}/objects/${tr}.png, error 6px

	@forEach [rows,rows-styled,no-column-border-styled.head,no-column-border-styled.body] as part
		${part}:
			image file dumps/${path}/objects/${part}.png, error 76px

		@forEach [${part}.tr-*] as tr
			${tr}:
				image file dumps/${path}/objects/${tr}.png, error 30px

	@forEach [sort] as part
		${part}.sort-button-1.sort-icon, ${part}.sort-button-2.sort-icon:
			# The 1px is to compensate for the border
			aligned horizontally centered ${part}.tr-1 1px

		${part}.sort-button-3.sort-icon:
			absent

	small.head:
		above small.body 0px
		aligned horizontally top small

	small.body:
		above small.foot 0px

	small.foot:
		aligned horizontally bottom small
