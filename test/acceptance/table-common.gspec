@groups
	imageCompare small.head,small.body,small.foot,simple.head,simple.body,simple.foot,no-column-border.head,no-column-border.body,tr-last-of-type.head,tr-last-of-type.body,notbody
	all          &imageCompare,wide,sort,rows,rows-styled,no-column-border-styled.head,no-column-border-styled.body

== D2L Table ==

	@forEach [&all] as part
		${part}.tr-1:
			aligned horizontally top ${part}

		@forEach [${part}.tr-*] as tr, prev as prevTr
			${tr}:
				below ${prevTr} 0px

	@forEach [&imageCompare] as part
		${part}:
			image file dumps/${path}/objects/${part}.png, error 12px

		@forEach [${part}.tr-*] as tr
			${tr}:
				image file dumps/${path}/objects/${tr}.png, error 6px

	@forEach [rows] as part
		${part}:
			image file dumps/${path}/objects/${part}.png, error 92px

		@forEach [${part}.tr-*] as tr
			${tr}:
				image file dumps/${path}/objects/${tr}.png, error 40px

	@on native-css
		@forEach [rows-styled,no-column-border-styled.head,no-column-border-styled.body] as part
			${part}:
				image file dumps/${path}/objects/${part}.png, error 92px

			@forEach [${part}.tr-*] as tr
				${tr}:
					image file dumps/${path}/objects/${tr}.png, error 40px

	@on 1.x
		@on rtl
			wide.right-action:
				inside partly wide 10px top, -15px left
				image file dumps/${path}/objects/wide.right-action.png, error 20px
		@on ltr
			wide.right-action:
				inside partly wide 10px top, 275px left
				image file dumps/${path}/objects/wide.right-action.png, error 20px

	small.head:
		above small.body 0px
		aligned horizontally top small

	small.body:
		above small.foot 0px

	small.foot:
		aligned horizontally bottom small
